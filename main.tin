#NOP VSOF;
#NOP -------------------------------------------------------------------------;
#NOP File principale, carica e predisponi tutto;
#NOP -------------------------------------------------------------------------;

#NOP -- Importa le librerie         --;
#var lib lib
#var mod modules
#read $lib/colors.tin
#read $lib/filesystem.tin
#read $lib/modloader.tin

#NOP -- Carica i moduli di /modules --;
load_module functions
load_module syslog
load_module events
load_module help
load_module counter
load_module telnet
load_module msdp
load_module mxp
load_module mslp
load_module gmcp
load_module macro
load_module alias
load_module triggers
load_module map
load_module gui
load_module comm_bar;
load_module side_bar;
load_module bars;

#NOP -------------------------------------------------------------------------;
#NOP All'avvio chiede quale mud e personaggio usare;
#NOP -------------------------------------------------------------------------;

#EVENT {PROGRAM START}
{
    #NOP CONFIG {SCROLL_LOCK} {ON};
    #NOP CONFIG {VERBOSE} {ON};
    #NOP CONFIG {DEBUG TELNET} {ON};
    #CONFIG {PACKET PATCH} {0.5};
    #CONFIG {MOUSE} {ON};
    #CONFIG {MOUSE TRACKING} {ON};
    #NOP INFO {BUTTON} {ON};
    #NOP INFO {EVENT} {ON};

    #CLASS worlddata read saves/muddata.dat;
    #if {&{worldlist} == 0}
    {
        #class worlddata open;
        #list worldlist clear;
        #class worlddata close;
    };

    #READ motd.tin;

    #ECHO {%h} {{ LUMEN ET UMBRA }};
    #ECHO { };
    #ECHO { CONNECT e il nome del personaggio};
    #ECHO { ADDPRG nome personaggio password};
    #ECHO { DELPRG nome del pg per cancellare};
    #ECHO { };
    #ECHO {%h};
    connect
}

#ALIAS {addprg}
{
    #if {&worldlist[%1][char] == "%1"}
    {
        #LINE ignore #SHOWME {Personaggio già presente}
    };
    #elseif {"%1" == "" | "%2" == ""}
    {
        #LINE ignore #SHOWME {Sintassi: addprg <nome personaggio> <password>}
    };
    #else
    {
        #VARIABLE worldlist[%1] %1;
        #VARIABLE worldlist[%1][char] %1;
        #VARIABLE worldlist[%1][pass] %2;
        #LINE ignore #SHOWME {Aggiunto: $worldlist[%1][char]};
        #CLASS worlddata write saves/muddata.dat
    };
    connect
}

#ALIAS {delprg}
{
    #if {&worldlist[%1] != 0}
    {
        #UNVAR worldlist[%1];
        #CLASS worlddata write saves/muddata.dat;
        #LINE ignore #SHOWME Il mud %1 è stato cancellato.
    };
    #else
    {
        #ECHO {Il personaggio %s non è stato trovato nella lista.} {%1}
    };
    connect
}

#ALIAS {connect}
{
    #if {"%1" == ""}
    {
        #ECHO {%h} {{ MUD LIST }};
        #ECHO { };
        #foreach {*worldlist[]} {world}
        {
            #ECHO { %-10s %s} {$worldlist[$world][char]} {\e]68;2;EXEC;connect $world\a\e[4mconnect\e[24m};
        };
        #ECHO { };
        #ECHO {%h};
    };
    #elseif {&worldlist[%1] != 0}
    {
        #VARIABLE mud {%1};
        #SESSION leu new-leu.leu.it 6000
    };
    #else
    {
        #ECHO {Non è stato possibile trovare %1.}
    };
}

#NOP -------------------------------------------------------------------------;
#NOP Eventi di sessione;
#NOP -------------------------------------------------------------------------;

#EVENT {SESSION CONNECTED}
{
    #VARIABLE mud @gts{$mud};
    #if {&worldlist[$mud]}
    {
        #if {&worldlist[$mud][char]}
        {
            #SEND {$worldlist[$mud][char]};
            #if {&worldlist[$mud][pass]}
            {
                #SEND {$worldlist[$mud][pass]};
                gui_init;
                gui_refresh;
            };
        };
    };
}

#EVENT {SESSION DISCONNECTED}
{
    #MAP write saves/map.dat
}

#EVENT {SESSION DESTROYED}
{
    #gts #delay 0 #end
}

#NOP -------------------------------------------------------------------------;
#NOP EVENTI VARI;
#NOP -------------------------------------------------------------------------;

#EVENT {IAC WILL MSDP}
{
    #SEND {$TELNET[IAC]$TELNET[DO]$TELNET[MSDP]\};
    #delay 1 msdp_report
}

#EVENT {IAC SB MSDP}
{
    #VARIABLE {MSDP_%0} {%1}
}

#EVENT {IAC SB MSDP SCREEN_WIDTH}
{
    gui_refresh;
}

#EVENT {IAC SB MSDP SCREEN_HEIGHT}
{
    gui_refresh
}

#EVENT {IAC SB MSDP AC}
{
    gui_refresh
}

#EVENT {IAC SB NAWS}
{  
    gui_refresh
}

#EVENT {SCREEN RESIZE}
{
    gui_refresh;
    #SCREEN refresh;
    #BUFFER end
}

#EVENT {SCREEN ROTATE LANDSCAPE}
{
    gui_refresh
}

#EVENT {SCREEN ROTATE PORTRAIT}
{ 
    gui_refresh
}

#EVENT {RECEIVED LINE}
{
    gui_line_update
}

#EVENT {RECEIVED ERROR}
{
	#echo <118>%h { TOKENIZER };
	#info tokenizer -1;
	#echo <118>%h;
}

#EVENT {IAC WILL GMCP}
{
    #SEND {$TELNET[IAC]$TELNET[DO]$TELNET[GMCP]\};
}

#NOP -------------------------------------------------------------------------;
#NOP Se rientri da ld richiedi al mud le variabili msdp;
#NOP -------------------------------------------------------------------------;

#ACTION {Ritrovi il senso della realta' ma sei un po' stordito.}
{
    msdp_ask_hpbar
}

#NOP -------------------------------------------------------------------------;
#NOP Use mouse click to change the input cursor's position.;
#NOP -------------------------------------------------------------------------;

#EVENT {SHORT-CLICKED MOUSE BUTTON ONE -1}
{
	#cursor position %1
}

#NOP -------------------------------------------------------------------------;
#NOP LOG on or off;
#NOP -------------------------------------------------------------------------;
#ALIAS {log}
{
     #if {"%0" == "on"}
     {
          #if {"$logging" != "on"}
          {
               #FORMAT {logfile} {logs/%t.html} {%Y-%m-%d};
               #log append $logfile;
               #VARIABLE logging on;
          };
          #else
          {
               #LINE ignore #SHOWME Stai già facendo un log.;
          }
     };
     #elseif {"%0" == "off"}
     {
          #if {"$logging" == "on"}
          {
               #log off;
               #VARIABLE logging off;
          };
          #else
          {
               #LINE ignore #SHOWME You are not logging.;
          };
     };
     #else
     {
          #LINE ignore #SHOWME Turn logging on or off?;
     };
}
