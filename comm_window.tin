#NOP VSOF;
#nop File di gestione della finestra delle comunicazioni e messaggi;

#VARIABLE {comm_window}
{
    {active} {msg}
    {botcol} {-1}
    {botrow} {7}
    {offset} {1}
    {tab} 
    {
        {msg} {}
        {grp} {}
        {gld} {}
    }
    {topcol} {1}
    {toprow} {1}
    {unread} 
    {
        {msg} {0}
        {grp} {0}
        {gld} {0}
    }
}

#ALIAS {comm_window_scroll_up}
{
    #if {$comm_window[offset] < &comm_window[tab][%0][]}
    {
        #math comm_window[offset] $comm_window[offset] + 1;
        comm_window_draw_data $comm_window[active]
    };
}

#BUTTON {$comm_window[toprow] $comm_window[topcol] $comm_window[botrow] $comm_window[botcol] PRESSED MOUSE BUTTON ONE}
{
    comm_window_scroll_up $comm_window[active];
}

#BUTTON {$comm_window[toprow] $comm_window[topcol] $comm_window[botrow] $comm_window[botcol] PRESSED MOUSE BUTTON THREE}
{
    comm_window_scroll_down;
}

#FUNCTION {comm_window_square}
{
    #return $comm_window[toprow]+1 $comm_window[topcol]+1 $comm_window[botrow]-1 $comm_window[botcol]-1;
}

#FUNCTION {comm_window_border}
{
    #return $comm_window[toprow] $comm_window[topcol] $comm_window[botrow] $comm_window[botcol];
}

#nop -------------------------------------------------------------------------;
#nop Alias della finestra di comunicazione;
#nop -------------------------------------------------------------------------;

#ALIAS {comm_window_scroll_down}
{
    #if {$comm_window[offset] > 1}
    {
        #math comm_window[offset] $comm_window[offset] - 1;
        comm_window_draw_data $comm_window[active];
    };
}

#ALIAS {comm_window_draw_data}
{
    #draw tile @comm_window_square{} $comm_window[tab][%1][-$comm_window[offset]-10..-$comm_window[offset]];
}

#ALIAS {comm_window_show %1 %2}
{
    #list comm_window[tab][%1] ins -1 {%2};
    #if {{%1} === {$comm_window[active]}}
    {
        comm_window_draw_data %1;
    };
    #elseif {$comm_window[unread][%1] == 0}
    {
        #variable comm_window[unread][%1] 1;
        comm_window_draw_tabs;
    };
}

#ALIAS {comm_window_draw_tabs}
{
    #draw green rounded box @comm_window_border{};
    #local short {};
    #local index {};
    #loop {1} {&comm_window[tab][]} {index}
    {
        #local tab *comm_window[tab][+$index];
        #if {{$comm_window[active]} === {$tab}}
        {
            #format short <138>%.6s $tab;
        };
        #elseif {$comm_window[unread][$tab] == 1}
        {
            #format short <168>%.6s $tab;
        };
        #else
        {
            #format short <268>%.6s $tab;
        };
        #line ignore #showme {\e]68;2;TAB;comm_window_tab_click $tab\a\e[4;24m$short\e[24m} {$comm_window[toprow]} {$comm_window[topcol] - 5 + $index * 7}
    }
}

#ALIAS {comm_window_tab_click}
{
    #variable comm_window[active] %0;
    #variable comm_window[unread][%0] 0;
    #variable comm_window[offset] 1;
    comm_window_draw_tabs;
    comm_window_draw_data %0;
}

#nop -------------------------------------------------------------------------;
#nop Azioni della finestra di comunicazione che catturano il dialogo;
#nop -------------------------------------------------------------------------;

#ACTION {~Tu dici %2}
{
    comm_window_show msg Tu dici a %1 %2;
}

#ACTION {~%1 ti dice %2}
{
    comm_window_show msg %1 ti dice a %2;
}

#ACTION {~%1 dice al gruppo %2}
{
    comm_window_show grp %1 dice %2;
}

#ACTION {~Tu dici al gruppo %2}
{
    comm_window_show grp %1 dice %2;
}

#ACTION {~%1 dice alle lame %2}
{
    comm_window_show gld %1 dice alle lame %2;
}

#ACTION {~%1 dice ai Luce %2}
{
    comm_window_show gld %1 dice ai Luce %2;
}

#ACTION {~%1 dice ai Mercenari %2}
{
    comm_window_show gld %1 dice ai Mercenari %2;
}

#ACTION {~%1 dice agli Esploratori %2}
{
    comm_window_show gld %1 dice agli Esploratori %2;
}

#ACTION {~%1 dice ai Vampiri %2}
{
    comm_window_show gld %1 dice ai Vampiri %2;
}

#ACTION {~%1 dice ai Jedi %2}
{
    comm_window_show gld %1 dice ai Jedi %2;
}

#ACTION {~%1 dice alla gilda %2}
{
    comm_window_show gld %1 dice agli alla gilda %2;
}

#ACTION {~%1 dice algli spettri %2}
{

    comm_window_show gld %1 dice agli spettri %2;
}

#ACTION {~Mandi a %1 il pensiero %2}
{
    comm_window_show msg Tu dici a %1 %2;
}

#ACTION {~%1 ti manda il pensiero %2}
{
    comm_window_show msg Tu mandi il pensiero a %1 %2;
}

#ACTION {~Mandi a %1 il messggio %2}
{
    comm_window_show msg Tu mandi il messaggio a %1 %2;
}

#ACTION {~%1 ti manda il messaggio %2}
{
    comm_window_show msg ti manda il messaggio a %1 %2;
}
