#NOP VSOF;
#nop -------------------------------------------------------------------------;
#nop Azioni che cancellano la creazine della stanza nella mappa se c'e' un ostacolo;
#nop -------------------------------------------------------------------------;

#ACTION {Non puoi andare da quella parte.} {#map undo}
#ACTION {Non puoi! Stai combattendo per la tua vita!} {#map undo}

#nop -------------------------------------------------------------------------;
#nop Inizializza la mappa;
#nop -------------------------------------------------------------------------;

#ALIAS {map_start}
{
    #map flag vtmap;
    #map flag unicode;
    #map goto 1
}

#EVENT {MAP SHORT-CLICKED MOUSE BUTTON ONE}
{
    #map run {%0} 0.25
}

#nop -----------------------------------------------------------------------;
#nop Quando si disconnette salva la mappa
#nop -----------------------------------------------------------------------;

#EVENT {SESSION DISCONNECTED}
{
    #map write saves/map.dat
}

#nop -------------------------------------------------------------------------;
#nop Script per seguire un pergorso fino la sua fine es trave East.;
#nop -------------------------------------------------------------------------;

#VARIABLE {travel} {0}

#VARIABLE {revdir}
{
     {Down} {Up}
     {East} {West}
     {North} {South}
     {South} {North}
     {Up} {Down}
     {West} {East}
}
#VARIABLE {longdir}
{
     {d} {Down}
     {e} {East}
     {n} {North}
     {s} {South}
     {u} {Up}
     {w} {West}
}
#VARIABLE {shortdir}
{
     {Down} {d}
     {East} {e}
     {North} {n}
     {South} {s}
     {Up} {u}
     {West} {w}
}
#FUNCTION {finddir}
{
     #if {"%1" != "%2"}
     {
          #return %2
     };
     #else
     {
          #return %3
     };
}

#ALIAS {travel}
{
     #if {$travel}
     {
          #variable travel 0;
          #showme <118>Ti fermi dal tuo viaggio.
     };
     #else
     {
          #variable travel 1;
          #variable lastdir $revdir[$longdir[%0]];
          #showme <118>Viaggi a %0.;
          %0
     };
}

#ACTION {Uscite: %1}
{
     #if {$travel}
     {
          #if {"%1" != "%* %*"}
          {
               #variable travel 0;
               #showme <118>Ti fermi nel tuo viaggio per colpa di un vicolo ceco.
          };
          #else
          {
               #regexp {%1} {%* %* %*}
               {
                    #var travel 0;#showme <118>Ti fermi nel tuo viaggio per decidere una direzione.
               };
               #else
               {
                    #regexp {%1} {%* %*}
                    {
                         #var newdir @finddir{$lastdir;&1;&2};#showme <118>Viaggi verso $newdir.;#var lastdir $revdir[$newdir];$shortdir[$newdir]
                    }
               }
          };
     };
}
{1}
