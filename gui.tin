#NOP VSOF;
#NOP -------------------------------------------------------------------------;
#NOP struttura della gui;
#NOP -------------------------------------------------------------------------;
#VARIABLE {gui}
{
     {rows} {40}
     {cols} {80}
     {top} {1}
     {bot} {5}
     {left} {1}
     {right} {0}
}

#VARIABLE {title_bar}
{
     {toprow} {1}
     {topcol} {1}
}

#VARIABLE {bottom_bar}
{
     {toprow} {-6}
     {topcol} {1}
     {botrow} {-2}
     {botcol} {-1}
}


#NOP -------------------------------------------------------------------------;
#NOP Inizializza l'interfaccia grafica;
#NOP -------------------------------------------------------------------------;
#ALIAS {gui_init}
{
    #READ {map.tin};
    #READ {comm_bar.tin};
    #READ {side_bar.tin};
    #MAP create;
    map_start;
    #VAR hasLogged 1
    gui_refresh;
    title_bar_draw;
    load_alias_sidebar
}

#NOP -------------------------------------------------------------------------;
#NOP Ridimensiona l'interfaccia grafica
#NOP -------------------------------------------------------------------------;
#ALIAS {resize_gui}
{
    #if {&hasLogged != 0} 
    {
      #SCREEN get rows {gui[rows]};
      #SCREEN get cols {gui[cols]};
      #if {$gui[rows] > 30 && $gui[cols] > 100 && $gui[cols] < 200} {
        #SPLIT {$gui[rows]/5} {$gui[bot]} {$gui[left]} -100 1;
        #MAP flag vtmap on;
        #MATH {gui[right]} {$gui[cols] - 100};
        #MAP offset {$gui[rows]/3+2} {-$gui[right]+3} -10 -3;
        #DRAW white calign box {$gui[rows]/3+1} {-$gui[right]+2} -7 -1 ;
        #VAR {comm_bar[botrow]} {$gui[rows]/5};
        #MATH {comm_bar[botcol]} {-$gui[right]};
        #VAR {side_bar[botrow]} {$gui[rows]/3};
        #MATH {side_bar[topcol]} {-$gui[right]+2};
        #SCREEN clear top;
        comm_bar_draw_data $comm_bar[active];
        #delay {0.5} {comm_bar_draw_tabs};
        side_bar_draw_data $side_bar[active];
        #delay {0.5} {side_bar_draw_tabs}
        };    
      #elseif {$gui[rows] > 30 && $gui[cols] > 200} {
        #SPLIT {$gui[rows]/5} {$bottom_bar[toprow]} 1 {-$gui[cols]/3} 1;
        #MAP flag vtmap on;
        #MAP offset {$gui[rows]/3+2} {$gui[cols]/2+5} -10 -3;
        #VAR {comm_bar[botrow]} {$gui[rows]/5};
        #MATH {comm_bar[botcol]} {$gui[cols]/2};
        #SCREEN clear top;
        #delay {0.5} {comm_bar_draw_tabs}
        };
      #elseif {$gui[rows] > 30 && $gui[cols] < 100} {
        #MAP flag vtmap on;
        #SCREEN clear all;
        #SPLIT 13 {$bottom_bar[toprow]} 0 0 1;
        #MAP offset 8 1 14 -1;
        #MATH {comm_bar[botcol]} {$gui[cols]};
        #VAR {comm_bar[botrow]} 7;
        #delay {0.5} {comm_bar_draw_tabs}
        };
      #else {
        #MAP flag vtmap off;
        #SCREEN clear all; 
        #SPLIT 1 {$bottom_bar[toprow]} 0 0 1;
        };
    }
}

#NOP -------------------------------------------------------------------------;
#NOP Aggiorna l'interfaccia grafica;
#NOP -------------------------------------------------------------------------;
#ALIAS {gui_refresh}
{
    #if {&hasLogged != 0} 
    {
      resize_gui;
      title_bar_draw;
      bottom_bar_draw;
      #BUFFER refresh;
    }
}

#NOP -------------------------------------------------------------------------;
#NOP Quando si riceve una linea dal mud aggiorna.;
#NOP -------------------------------------------------------------------------;
#ALIAS {gui_line_update}
{
    #if {&hasLogged != 0} 
    {
      bottom_bar_draw;
      #MAP update
    }
}

#nop -------------------------------------------------------------------------;
#nop Draw some buttons over the health bar.;
#nop -------------------------------------------------------------------------;
#ALIAS {title_bar_draw}
{
    #line ignore #showme {* $worldlist[$mud][name] * $worldlist[$mud][char]} {1};
    #line ignore #showme {\e]68;2;TAB;#end\a\e[4;24mEsci\e[24m} {1} {-4};
}

#nop -------------------------------------------------------------------------;
#nop Draw a health bar.;
#nop -------------------------------------------------------------------------;
#alias {bottom_bar_draw}
{
    #math {hp_percent} {100 * $MSDP_HEALTH / $MSDP_HEALTH_MAX};
    #math {hpbar1}   {$hp_percent / 10};
    #math {hpbar2}   {10 - $hpbar1};
    #math {mana_percent} {100 * $MSDP_MANA / $MSDP_MANA_MAX};
    #math {manabar1}   {$mana_percent / 10};
    #math {manabar2}   {10 - $manabar1};
    #math {mov_percent} {100 * $MSDP_MOVEMENT / $MSDP_MOVEMENT_MAX};
    #math {movbar1}   {$mov_percent / 10};
    #math {movbar2}   {10 - $movbar1};

    #FORMAT {hpbar} {<011>%+${hpbar1}s<099><000>%+${hpbar2}s<099>};
    #FORMAT {manabar} {<015>%+${manabar1}s<099><000>%+${manabar2}s<099>};
    #FORMAT {movbar} {<012>%+${movbar1}s<099><000>%+${movbar2}s<099>};

    #FORMAT prompt1 {  %s * hp: %s/%s} $hpbar $MSDP_HEALTH $MSDP_HEALTH_MAX;
    #FORMAT prompt2 {  %s * ma: %s/%s} $manabar $MSDP_MANA $MSDP_MANA_MAX;
    #FORMAT prompt3 {  %s * mv: %s/%s} $movbar $MSDP_MOVEMENT $MSDP_MOVEMENT_MAX;
 
	#LINE ignore #SHOWME {$prompt1} {-5};
	#LINE ignore #SHOWME {$prompt2} {-4};
	#LINE ignore #SHOWME {$prompt3} {-3};

    #DRAW white calign box {$bottom_bar[toprow]} {$bottom_bar[topcol]} {$bottom_bar[botrow]} {$bottom_bar[botcol]}
}
